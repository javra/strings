{-# OPTIONS --prop --rewriting --allow-unsolved-metas #-}

open import Diags
open import Arity
open import Sliding
open import Identities

/nsuc : ∀{n} → /n {suc n} ~ ∣ ⊗ /n · / ⊗ ∣ ^⊗ n
/nsuc {zero} = ·∣∣ ■ ε⊗ ■ - ⊗ε ■ - ∣∣·
/nsuc {suc n} = ·~ (~⊗ (/nsuc {n}) ■ - ⊗∣·⊗∣ ■ ·~ (-⊗⊗ ■ ⊗~ (- ∣^⊗suc)) ■ ~· -⊗⊗)
                ■ ·· ■ ~· (~· -⊗⊗ ■ ∣⊗·∣⊗)

/n+ : ∀{m n} → /n {m + n} ~ ∣ ^⊗ m ⊗ /n {n} · /n {m} ⊗ ∣ ^⊗ n
/n+ {m} {zero} = - (~· (- ∣^⊗suc) ■ ∣n· ■ ⊗ε)
/n+ {m} {suc n} = ·~ (~⊗ (/n+ {m} {n}) ■ - ⊗∣·⊗∣ ■ ·~ (-⊗⊗ ■ ⊗~ (- ∣^⊗suc))) ■ ··
                  ■ ~· (~· (~⊗ ∣^⊗+ ■ -⊗⊗) ■ ·~ -⊗⊗ ■ ∣n⊗·∣n⊗)

-- Naturality of /n
/n·∣⊗ : ∀{m n}{d : D m n} → /n · ∣ ⊗ d ~ d ⊗ ∣ · /n
/n·∣⊗ {d = ε}     = ·~ (⊗ε) ■ - (~· (ε⊗))
/n·∣⊗ {d = ∣}     = ·∣∣ ■ - ∣∣·
/n·∣⊗ {d = d · e} = ·~ (- ∣⊗·∣⊗) ■ ·· ■ ~· /n·∣⊗ ■ -·· ■ ·~ /n·∣⊗
                    ■ ·· ■ ~· ⊗∣·⊗∣
/n·∣⊗ {d = _⊗_ {m} {n} {k} {l} d e}
                  = ~· (/n+ {m}{k})
                    ■ -·· ■ ·~ (·~ (⊗⊗) ■ - (·⊗·) ■ ~⊗ /n·∣⊗ ■ ·⊗·)
                    ■ ·· ■ ~· (·~ -⊗⊗ ■ ∣n⊗·⊗∣m) ■ ·~ (- ∣n⊗·⊗∣m) ■ ··
                    ■ ~· (·~ (~⊗ ∣^⊗suc ■ -⊗⊗)
                           ■ - (·⊗·) ■ ⊗~ /n·∣⊗ ■ ·⊗· ■ ~· (⊗⊗))
                    ■ -·· ■ ·~ (- (/n+ {n} {l}))
/n·∣⊗ {d = ∩}     = ∣· ■ - (∩∣∣//∣) ■ -··
                    ■ ·~ (~· (~⊗ (- (⊗ε))) ■ ·~ (~⊗ (- (·∣∣ ■ ε⊗))))
/n·∣⊗ {d = ∪}     = ~· (~· (~⊗ (⊗ε)) ■ ·~ (~⊗ (·∣∣ ■ ε⊗)))
                    ■ ∣//∣∣∪ ■ - ·∣
/n·∣⊗ {d = /}     = ~· (~· (~⊗ (⊗ε)) ■ ·~ (~⊗ (·∣∣ ■ ε⊗))) ■ - (///)
                    ■ -·· ■ ·~ (~· (~⊗ (- (⊗ε))) ■ ·~ (- (~⊗ (·∣∣ ■ ε⊗))))
/n·∣⊗ {d = R}     = /nR
/n·∣⊗ {d = M}     = ·~ /M ■ ∣· ■ ·~ (- ε⊗ ■ - ·∣∣)
/n·∣⊗ {d = B ns}  = /nB

/-nsuc : ∀{n} → /-n {suc n} ~ /-n ⊗ ∣ · ∣ ^⊗ _ ⊗ /
/-nsuc {zero}  = ~· (⊗ε) ■ ·∣∣ ■ - ∣∣· ■ ·~ (- (ε⊗))
/-nsuc {suc n} = ·~ (⊗~ (/-nsuc {n}) ■ - ∣⊗·∣⊗ ■ ·~ (⊗⊗)) ■ ··
                 ■ ~· (~· (⊗~ ∣^⊗suc ■ ⊗⊗) ■ ·~ (⊗⊗) ■ ⊗∣·⊗∣)

/-n+ : ∀{m n} → /-n {m + n} ~ /-n {m} ⊗ ∣ ^⊗ n · ∣ ^⊗ m ⊗ /-n {n}
/-n+ {zero}  {n} = - (∣n· ■ ε⊗)
/-n+ {suc m} {n} = ·~ (⊗~ (/-n+ {m} {n}) ■ - ∣⊗·∣⊗ ■ ·~ (⊗⊗)) ■ ··
                   ■ ~· (~· (⊗~ (∣^⊗+ {m}{n}) ■ ⊗⊗) ■ ·~ (⊗⊗) ■ ⊗∣n·⊗∣n)

-- Naturality of /-n
/-n·⊗∣ : ∀{m n}{d : D m n} → /-n · d ⊗ ∣ ~ ∣ ⊗ d · /-n
/-n·⊗∣ {d = ε}     = ∣· ■ ε⊗ ■ - (⊗ε) ■ - ·∣
/-n·⊗∣ {d = ∣}     = ·∣∣ ■ - ∣∣·
/-n·⊗∣ {d = d · e} = ·~ (- ⊗∣·⊗∣) ■ ·· ■ ~· /-n·⊗∣ ■ -·· ■ ·~ /-n·⊗∣
                     ■ ·· ■ ~· ∣⊗·∣⊗
/-n·⊗∣ {d = _⊗_ {m}{n}{k}{l} d e} = ~· (/-n+ {m}{k})
                     ■ -·· ■ ·~ (·~ -⊗⊗ ■ - (·⊗·) ■ ⊗~ /-n·⊗∣ ■ ·⊗·)
                     ■ ·· ■ ~· (·~ (⊗⊗ ■ ~⊗ (- ∣^⊗suc)) ■ ⊗∣n·∣m⊗)
                     ■ ·~ (- ⊗∣n·∣m⊗) ■ ·· ■ ~· (·~ (⊗⊗) ■ - (·⊗·) ■ ~⊗ /-n·⊗∣)
                     ■ ~· (·⊗· ■ ~· -⊗⊗) ■ -·· ■ ·~ (- (/-n+ {n} {l}))
/-n·⊗∣ {d = ∩}     = - (·· ■ ·~ (⊗~ (·∣∣ ■ ⊗ε)) ■ ~· (·~ (⊗~ (⊗ε))) ■ ∣∩/∣∣/ ■ - ∣·)
/-n·⊗∣ {d = ∪}     = ~· (~· (⊗~ (⊗ε)) ■ ·~ (⊗~ (·∣∣ ■ ⊗ε))) ■ /∣∣/∪∣ ■ - ·∣
/-n·⊗∣ {d = /}     = ~· (~· (⊗~ (⊗ε)) ■ ·~ (⊗~ (·∣∣ ■ ⊗ε))) ■ ///
                     ■ - (·~ (~· (⊗~ (⊗ε)) ■ ·~ (⊗~ (·∣∣ ■ ⊗ε))) ■ ··)
/-n·⊗∣ {d = R}     = /-nR
/-n·⊗∣ {d = M}     = ·~ /-M ■ ∣· ■ ·~ (- ⊗ε ■ - ·∣∣)
/-n·⊗∣ {d = B ns}  = /-nB

∩∣n/n∪∣n : ∀{n} → ∩ ⊗ ∣ ^⊗ (suc n) · ∣ ⊗ /n {suc n} · ∪ ⊗ ∣ ^⊗ (suc n) ~ /n {n}
∩∣n/n∪∣n {n} = ~· (·~ (⊗~ (/nsuc {n}) ■ - ∣⊗·∣⊗) ■ ··
                    ■ ~· (·~ (⊗⊗) ■ - (·⊗·) ■ ~⊗ ·∣∣ ■ ⊗~ (∣n·)) ■ ·~ (⊗⊗))
               ■ ∩⊗·∣/⊗·∪⊗ ■ ·∣n ■ ·∣n

∣n∩/-n∣n∪ : ∀{n} → ∣ ^⊗ (suc n) ⊗ ∩ · /-n {suc n} ⊗ ∣ · ∣ ^⊗ (suc n) ⊗ ∪ ~ /-n {n}
∣n∩/-n∣n∪ = ~· (·~ (~⊗ /-nsuc ■ - ⊗∣·⊗∣ ■ ~· -⊗⊗) ■ ·· ■ ~· (- (·⊗·) ■ ⊗~ ·∣∣))
            ■ ⊗∩·⊗/∣·⊗∪ ■ ·∣n ■ ·~ (- ∣^⊗suc) ■ ·∣n ■ ∣n·

-- The downwards bracket over n strings
∩n : ∀{n} → D n (n + 2)
∩n {n} = ∣ ^⊗ n ⊗ ∩ · /n ⊗ ∣

∩n·∣⊗∣ : ∀{m n}{d : D m n} → ∩n · ∣ ⊗ d ⊗ ∣ ~ d · ∩n
∩n·∣⊗∣ = -·· ■ ·~ (- (·⊗·) ■ ~⊗ /n·∣⊗ ■ ·⊗·)
         ■ ·· ■ ~· (·~ -⊗⊗ ■ - (·⊗·) ■ ~⊗ (∣n·) ■ ⊗~ ·∣∣)
         ■ ~· (~⊗ (- (·∣n)) ■ - ·⊗∩) ■ -··

-- The upwards bracket over n strings
∪n : ∀{n} → D (n + 2) n
∪n {n} = ∣ ⊗ /n · ∪ ⊗ ∣ ^⊗ n

∣⊗∣·∪n : ∀{m n}{d : D m n} → ∣ ⊗ d ⊗ ∣ · ∪n ~ ∪n · d
∣⊗∣·∪n = ·· ■ ~· (~· -⊗⊗ ■ - (·⊗·) ■ ⊗~ (- /n·∣⊗) ■ ·⊗·) ■ -··
         ■ ·~ (~· (⊗⊗) ■ - (·⊗·) ■ ⊗~ (·∣n) ■ ~⊗ ∣∣· ■ ⊗~ (- (∣n·)) ■ - ∪⊗·) ■ ··

-- The downwards bracket under n strings
∩-n : ∀{n} → D n (n + 2)
∩-n {n} = ∩ ⊗ ∣ ^⊗ n · ∣ ⊗ /-n

∩-n·∣⊗∣ : ∀{m n}{d : D m n} → ∩-n · ∣ ⊗ d ⊗ ∣ ~ d · ∩-n
∩-n·∣⊗∣ = -·· ■ ·~ (·~ -⊗⊗ ■ - (·⊗·) ■ ⊗~ /-n·⊗∣ ■ ·⊗·) ■ ·· ■ ~· (·~ (⊗⊗) ■ ⊗∣n·∣∣⊗)
          ■ ~· (- ·⊗∣n) ■ -··

-- The upwards bracket under n strings
∪-n : ∀{n} → D (n + 2) n
∪-n {n} = /-n ⊗ ∣ · ∣ ^⊗ n ⊗ ∪

∣⊗∣·∪-n : ∀{m n}{d : D m n} → ∣ ⊗ d ⊗ ∣ · ∪-n ~ ∪-n · d
∣⊗∣·∪-n = ·· ■ ~· (- (·⊗·) ■ ~⊗ (- /-n·⊗∣) ■ ·⊗·) ■ -··
          ■ ·~ (⊗∣∣·∣n⊗ ■ - ∣n⊗·) ■ ··

∩∣n∩·∪n : ∀{n} → ∩ ⊗ ∣ ^⊗ n ⊗ ∩ · ∪n ~ ∩n
∩∣n∩·∪n {n} = ·· ■ ~· (·~ (- ∣⊗·∣⊗) ■ ·· ■ ~· (·~ (⊗⊗) ■ - (·⊗·) ■ ⊗~ (∩/)
                                                      ■ ~⊗ (·∣n ■ - (∣n·)) ■ ⊗~ (- ·∣∣) ■ ·⊗·)) ■ -·· ■ -··
              ■ ·~ (~· (⊗⊗) ■ ·~ (~· (⊗~ (- ⊗∣·⊗∣) ■ - ∣⊗·∣⊗ ■ ~· (⊗⊗) ■ ·~ (⊗⊗)) ■ ·~ (⊗⊗ ■ ⊗~ ∣^⊗suc ■ ⊗⊗))
                     ■ ·~ (~· ⊗∣·⊗∣ ■ ⊗∣·⊗∣) ■ ⊗∣·⊗∣
                     ■ ~⊗ (·· ■ ~· (~· (-⊗⊗ ■ ⊗~ (- ∣^⊗suc)) ■ ·~ ∣⊗·∣⊗) ■ ·~ -⊗⊗ ■ ∩∣n/n∪∣n))

∩n·∪∣n∪ : ∀{n} → ∩n · ∪ ⊗ ∣ ^⊗ n ⊗ ∪ ~ ∪n
∩n·∪∣n∪ = ~· (- ∩∣n∩·∪n) ■ -·· ■ ·~ (- ∣⊗∣·∪n) ■ ··
          ■ ~· (~· (~⊗ (⊗⊗) ■ -⊗⊗) ■ ·~ (~⊗ (⊗~ -⊗⊗ ■ ⊗⊗) ■ -⊗⊗) ■ - (·⊗·) ■ ~⊗ (∩∪))
          ■ ~· (⊗~ (~· (~⊗ ∣^⊗suc ■ -⊗⊗) ■ ·~ -⊗⊗ ■ - (·⊗·) ■ ⊗~ (∪∩) ■ ~⊗ (·∣n) ■ - ∣^⊗suc)) ■ ∣n·

∩∣n∩·∪-n : ∀{n} → ∩ ⊗ ∣ ^⊗ n ⊗ ∩ · ∪-n ~ ∩-n
∩∣n∩·∪-n = ·· ■ ~· (·~ (- ⊗∣·⊗∣) ■ ·· ■ ~· (~· -⊗⊗ ■ ·~ -⊗⊗ ■ - (·⊗·) ■ ~⊗ (∩/)
                                                  ■ ⊗~ (·~ (-⊗⊗ ■ ⊗~ (- ∣^⊗suc)) ■ ·∣n ■ - (∣n·)) ■ ~⊗ (- ·∣∣) ■ ·⊗·))
           ■ -·· ■ -··
           ■ ·~ (·~ (~· (~⊗ (- ∣⊗·∣⊗) ■ - ⊗∣·⊗∣)) ■ ·· ■ ·~ -⊗⊗
           ■ ~· (~· (-⊗⊗ ■ ⊗~ (⊗⊗)) ■ ·~ (~· -⊗⊗ ■ ·~ -⊗⊗ ■ ∣⊗·∣⊗))
           ■ ~· (·~ (⊗~ ⊗∣·⊗∣) ■ ∣⊗·∣⊗) ■ ∣⊗·∣⊗ ■ ⊗~ ∣n∩/-n∣n∪)

∩-n·∪∣n∪ : ∀{n} → ∩-n · ∪ ⊗ ∣ ^⊗ n ⊗ ∪ ~ ∪-n
∩-n·∪∣n∪ = ~· (- ∩∣n∩·∪-n) ■ -·· ■ ·~ (- ∣⊗∣·∪-n) ■ ··
           ■ ~· (~· (~⊗ (⊗⊗) ■ -⊗⊗) ■ ·~ (~⊗ (⊗~ -⊗⊗ ■ ⊗⊗) ■ -⊗⊗) ■ - (·⊗·) ■ ~⊗ (∩∪))
           ■ ~· (⊗~ (~· (~⊗ ∣^⊗suc ■ -⊗⊗) ■ ·~ -⊗⊗ ■ - (·⊗·) ■ ⊗~ (∪∩) ■ ~⊗ (·∣n)) ■ ⊗~ (- ∣^⊗suc)) ■ ∣n·

∪n~∪-n : ∀{m}{d : D m 0} → ∪n · d ~ ∪-n · d
∪n~∪-n = - ∣⊗∣·∪n ■ ·~ (·~ (⊗ε ■ - (ε⊗))) ■ ∣⊗∣·∪-n

∩n~∩-n : ∀{m}{d : D 0 m} → d · ∩n ~ d · ∩-n
∩n~∩-n = - ∩n·∣⊗∣ ■ ~· (~· (ε⊗ ■ - (⊗ε))) ■ ∩-n·∣⊗∣

∩n~∩-n' : ∀{m}{d : D (m + 2) 0} → ∩n · d ~ ∩-n · d
∩n~∩-n' = ~· (- ∩∣n∩·∪n) ■ -·· ■ ·~ ∪n~∪-n ■ ·· ■ ~· ∩∣n∩·∪-n

∪n~∪-n' : ∀{m}{d : D 0 (m + 2)} → d · ∪n ~ d · ∪-n
∪n~∪-n' = ·~ (- ∩n·∪∣n∪) ■ ·· ■ ~· ∩n~∩-n ■ -·· ■ ·~ ∩-n·∪∣n∪


